#!/usr/local/share/npm/bin/coffee

fs = require 'fs'
browserify = require 'browserify'
hamlc = require 'haml-coffee'
walk = require 'walk'
path = require 'path'
_ = require 'underscore'
coffee = require 'coffee-script'
browserifyTransform = require '../modules/browserify_transform'
mkdirp = require 'mkdirp'
{bundle} = require '../modules/client_bundle.coffee'

ROOT_DIR = path.normalize "#{__dirname}/.."
REPORTER_TEMPLATE = "#{ROOT_DIR}/test/reporter.hamlc"
TMP_DIR = "#{ROOT_DIR}/tmp"
TMP_REPORTER_PATH = "#{TMP_DIR}/reporter.html"
TMP_BUNDLE_PATH = "#{TMP_DIR}/bundle.js"
TMP_SPEC_DIR = "#{TMP_DIR}/spec"
SPEC_DIRECTORY = "#{ROOT_DIR}/test"
SPEC_REGEX = /.+_spec\.(js|coffee)/
SUPPORT_FILES = [
  "../public/javascripts/jquery.js",
  "../public/javascripts/underscore.js",
  "../public/javascripts/backbone.js",
  "../test/support/mocha.js",
  "../test/support/chai.js",
  "../test/support/sinon.js"]

# ----------------------------- Spec Files ----------------------------------#
specFiles = []
specWalker = walk.walk(SPEC_DIRECTORY)

specWalker.on 'file', (root, file, next) ->
  filePath = path.normalize "#{root}/#{file.name}"
  SPEC_REGEX.test(filePath) and specFiles.push(filePath)
  next()

#-------------------------------- Spec Bundle -------------------------------#
specWalker.on 'end', ->
  specFiles = specFiles.map (specFile) ->
    outPath = path.relative(SPEC_DIRECTORY, specFile).replace(/\.coffee$/, ".js")
    compiledPath = path.join TMP_SPEC_DIR, outPath
    mkdirp.sync path.dirname(compiledPath)
    fs.writeFileSync compiledPath, coffee.compile(fs.readFileSync(specFile).toString())
    path.normalize compiledPath
  compileCoffeescript()


#---------------------------- Compile Coffeescript ---------------------------#
compileCoffeescript = ->
  bundle (bundle) ->
    output = fs.createWriteStream TMP_BUNDLE_PATH
    bundle.pipe(output)
    renderReporterTemplate()

# Re-compile maximum once in 300 ms.
debouncedCompile = _.debounce(compileCoffeescript, 300)

# ---------------------------------- Watch files ------------------------------#

fs.watch "#{__dirname}/../client", (event, filename) ->
  debouncedCompile()

#----------------------------- Reporter Template -----------------------------#

renderReporterTemplate = ->
  reporterTemplate = fs.readFileSync(REPORTER_TEMPLATE).toString()
  fs.writeFileSync TMP_REPORTER_PATH, hamlc.compile(reporterTemplate)
    specFiles: specFiles
    bundlePath: TMP_BUNDLE_PATH
    supportPaths: SUPPORT_FILES
  process.stdout.write "[32mrendered reporter template\n"
