#!/usr/local/share/npm/bin/coffee

fs = require 'fs'
browserify = require 'browserify'
hamlc = require 'haml-coffee'
walk = require 'walk'
path = require 'path'
browserifyTransform = require '../modules/browserify_transform'

REPORTER_TEMPLATE = "#{__dirname}/../test/reporter.hamlc"
REPORTER_OUTPUT = "#{__dirname}/../tmp/reporter.html"
BUNDLE_PATH = "#{__dirname}/../tmp/bundle.js"
SPEC_DIRECTORY = "#{__dirname}/../test"
SPEC_REGEX = /.+_spec\.(js|coffee)/
SUPPORT_FILES = [
  "../test/support/mocha.js",
  "../test/support/chai.js",
  "../test/support/sinon.js"]

# ----------------------------- Spec Files ----------------------------------#
specFiles = []
walker = walk.walk(SPEC_DIRECTORY)

walker.on 'file', (root, file, next) ->
  filePath = path.normalize "#{root}/#{file.name}"
  SPEC_REGEX.test(filePath) and specFiles.push(filePath)
  next()

walker.on 'end', -> compileCoffeescript(specFiles)

#---------------------------- Compile Coffeescript ---------------------------#

compileCoffeescript = (files) ->
  b = browserify()
  b.transform browserifyTransform
  files.forEach (file) -> b.add file
  output = fs.createWriteStream BUNDLE_PATH
  b.bundle().pipe output
  renderReporterTemplate()

#----------------------------- Reporter Template -----------------------------#

renderReporterTemplate = () ->
  reporterTemplate = fs.readFileSync(REPORTER_TEMPLATE).toString()
  fs.writeFileSync REPORTER_OUTPUT, hamlc.compile(reporterTemplate)
    bundlePath: BUNDLE_PATH
    supportPaths: SUPPORT_FILES
